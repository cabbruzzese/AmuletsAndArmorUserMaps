defnum  NOT_SWITCHED    0
defnum  SWITCHED        1
defnum  ON              2
defnum  OFF             3

defvar  one             1
defvar	two				2
defvar	three			3
defvar	four			4
defvar  switch          5
defvar	gate			6
defvar  quakeactive		7
defvar	invasion		8

0:      //init
        Unblock(one) ;
        Unblock(two) ;
        Unblock(three) ;
		Unblock(four) ;
		Set(switch, NOT_SWITCHED) ;
		Set(gate, NOT_SWITCHED) ;
		Set(quakeactive, OFF) ;
		Set(invasion, OFF);
        End() ;

40: //Fireball Trap
		Block(three);
		Delay(4);
		GenerateMissile(4005, -1760, 2144, -96, -1760, 1888, -96, 64);
		Delay(4);
		GenerateMissile(4005, -1696, 2144, -96, -1696, 1888, -96, 64);
		Delay(16);//keep trap from firing in rapid fire succession in case earthquake happens while in doorway
		Unblock(three);
		End() ;

41: //Open secret door
		Block(one);
		Compare(switch, SWITCHED) ;
		If(Equal, 101) ;
		Set(switch, SWITCHED);
		SlideCeilingNice(156, -448, -320, 64, -1);
		Unblock(one);
		End();

42: //Close invasion door
		Block(two);
		Compare(gate, SWITCHED);
		If(Equal, 102);
		Set(gate, SWITCHED);
		DeactiveGenerator(0);//Disable invasion generator
		DeactiveGenerator(1);
		DeactiveGenerator(2);
		Sound(6004); //switch press sound
		SlideFloorNice(431, -120, -140, 16, -1);
		Delay(24);
		Sound(5002); //gate loud rumble sound
		SlideCeilingNice(433, 88, -64, 128, -1);
		Print("The invasion gate has been sealed.");
		Delay(196); 
		Sound(3005);
		Effect(29, 0, 128, 0); //Earthquake
		SlideFloorNice(427, -104,-592, 72, -1);
		Unblock(two);
		Gosub(43); //start the earthquakes forever
		End();

43: //Earthquake Primer
		Compare(gate, NOT_SWITCHED) ; //only prime if enabled
        If(Equal, 100);
		Compare(quakeactive, ON); //don't prime twice
		If(Equal, 100);
		Print("The ground is becoming unstable!");
		Set(quakeactive, ON);
		Gosub(44) ;
		End();

44: //Earthquake recurring effect
		Delay(2160) ;
		Sound(3005);
		Effect(29, 0, 72, 0);
		Delay(1000);
		Sound(3005);
		Effect(29, 0, 128, 0);
		Delay(500);
		Sound(3005);
		Effect(29, 0, 32, 0);
		Delay(128);
		Sound(3005);
		Effect(29, 0, 32, 0);
		Gosub(45);
		End();

45: //Earthquake Loop
		Compare(quakeactive, OFF); //double check earthquakes are still active
		If(Equal, 100);
		Gosub(44);
		End();

46: //Start invasion force generator
		Block(four);
		Compare(invasion, ON); //don't start multiple times
		If(Equal, 104);
		Compare(gate, SWITCHED); //don't start if gate is closed
		If(Equal, 104);
		Set(invasion, ON);
		ActivateGenerator(0);
		ActivateGenerator(1);
		ActivateGenerator(2);
		Unblock(four);
		End();

100:    //end all
        End() ;

101:    //end block 1
		Unblock(one);
        End() ;

102:    //end block 2
		Unblock(two);
        End() ;

103:    //end block 3
		Unblock(three);
        End() ;

104:    //end block 4
		Unblock(four);
        End() ;

201: //teleport to Mansion
		Teleport(-1728,-2830,0);
		End();

202: //teleport to grand hallway
		Teleport(-2656, 32, 0);
		End();

203: //teleport to Chest bones
		Teleport(192, -192, 0);
		End();

204: //teleport to Secret room
		Teleport(-2176, 1280, 0);
		End();

205: //teleport to starting door to outside chasm
		Teleport(32, 1248, 0);
		End();